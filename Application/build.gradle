plugins {

    id 'com.android.application'
    id 'kotlin-android'
    id("com.google.devtools.ksp") version "2.2.20-2.0.2"

    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
}

// Load signing configuration from .env or .env.properties file if they exist
def envProperties = new Properties()
def envFile = rootProject.file('.env')
def envPropertiesFile = rootProject.file('.env.properties')

// Try .env first, then .env.properties
if (envFile.exists()) {

    envFile.eachLine { line ->
        if (!line.startsWith('#') && line.contains('=')) {
            def parts = line.split('=', 2)
            if (parts.length == 2) {
                def key = parts[0].trim()
                def value = parts[1].trim()
                // Remove surrounding quotes if present
                if ((value.startsWith('"') && value.endsWith('"')) ||
                    (value.startsWith("'") && value.endsWith("'"))) {
                    value = value.substring(1, value.length() - 1)
                }
                envProperties.setProperty(key, value)
            }
        }
    }

} else if (envPropertiesFile.exists()) {

    envPropertiesFile.withInputStream { envProperties.load(it) }
}

// Process variable substitutions (e.g., $VAR_NAME)
envProperties.each { key, value ->

    if (value.startsWith('$')) {

        def refKey = value.substring(1)
        def refValue = envProperties.getProperty(refKey)

        if (refValue != null) {

            envProperties.setProperty(key, refValue)
        }
    }
}

// Helper function to get property from env file or system environment
def getEnvProperty = { String key ->

    return envProperties.getProperty(key) ?: System.getenv(key) ?: ""
}

android {

    compileSdk 36
    namespace "${shareconnect_application_name}"

    defaultConfig {

        minSdk 28
        targetSdk 36
        versionName "$shareconnect_version"
        versionCode shareconnect_version_code
        applicationId "$shareconnect_application_name"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        DEVELOPMENT {

            def devKeystorePath = getEnvProperty("SHARECONNECT_DEV_KEYSTORE_PATH")
            def devKeystoreFile = file(devKeystorePath.isEmpty() ? "Signing/dev.jks" : devKeystorePath)

            if (devKeystoreFile.exists()) {
                keyAlias getEnvProperty("SHARECONNECT_DEV_KEY_ALIAS")
                keyPassword getEnvProperty("SHARECONNECT_DEV_KEY_PASSWORD")
                storePassword getEnvProperty("SHARECONNECT_DEV_STORE_PASSWORD")
                storeFile devKeystoreFile
            }
        }

        CLOUD {

            def cloudKeystorePath = getEnvProperty("SHARECONNECT_CLOUD_KEYSTORE_PATH")
            def cloudKeystoreFile = file(cloudKeystorePath.isEmpty() ? "Signing/cloud.jks" : cloudKeystorePath)

            if (cloudKeystoreFile.exists()) {
                keyAlias getEnvProperty("SHARECONNECT_CLOUD_KEY_ALIAS")
                keyPassword getEnvProperty("SHARECONNECT_CLOUD_KEY_PASSWORD")
                storePassword getEnvProperty("SHARECONNECT_CLOUD_STORE_PASSWORD")
                storeFile cloudKeystoreFile
            }
        }
    }

    buildTypes {

        release {

            minifyEnabled true
            shrinkResources false
            // Only use signing config if keystore exists
            def cloudKeystorePath = getEnvProperty("SHARECONNECT_CLOUD_KEYSTORE_PATH")
            def cloudKeystoreFile = file(cloudKeystorePath.isEmpty() ? "Signing/cloud.jks" : cloudKeystorePath)
            if (cloudKeystoreFile.exists()) {
                signingConfig signingConfigs.CLOUD
            }
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'missing_rules.txt'
        }

        debug {

            minifyEnabled false
            shrinkResources false
            // Only use signing config if keystore exists
            def devKeystorePath = getEnvProperty("SHARECONNECT_DEV_KEYSTORE_PATH")
            def devKeystoreFile = file(devKeystorePath.isEmpty() ? "Signing/dev.jks" : devKeystorePath)
            if (devKeystoreFile.exists()) {
                signingConfig signingConfigs.DEVELOPMENT
            }
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {

        jvmTarget = '1.8'
    }

    packagingOptions {

        jniLibs {

            excludes += ['META-INF/*']
        }

        resources {

            excludes += ['META-INF/*', 'LICENSE-2.0.txt', 'META-INF/DEPENDENCIES',
                         'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE',
                         'META-INF/NOTICE.txt']
        }
    }

    sourceSets {
        debug {
            java.srcDirs += "build/generated/ksp/debug/kotlin"
        }
        release {
            java.srcDirs += "build/generated/ksp/release/kotlin"
        }
    }
}

dependencies {

    implementation project("${toolkit_context}:Main")
    implementation project("${toolkit_context}:Test")
    implementation project("${toolkit_context}:Analytics")

    testImplementation project("${toolkit_context}:Main")
    testImplementation project("${toolkit_context}:Test")

    androidTestImplementation project("${toolkit_context}:Main")
    androidTestImplementation project("${toolkit_context}:Test")

    // Room KSP compiler for database code generation
    ksp "androidx.room:room-compiler:2.8.0"

    // Unit Testing Dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'androidx.room:room-testing:2.8.0'

    // Instrumentation Testing Dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.mockito:mockito-android:4.6.1'
    androidTestImplementation 'androidx.room:room-testing:2.8.0'

    // UI Testing Dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.5.1'
}
